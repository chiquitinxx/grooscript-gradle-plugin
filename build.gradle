plugins {
    id "com.jfrog.bintray" version "1.0"
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'org.grooscript'
version = '0.9-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: "groovy-all"
    }
    compile 'cglib:cglib-nodep:2.2'
    compile 'org.grooscript:grooscript:0.6.3', {
        exclude module: "groovy"
    }
    //compile "org.springframework.boot:spring-boot-starter-websocket:1.2.0.RELEASE"
    compile "org.springframework:spring-websocket:4.1.3.RELEASE"
    compile "org.springframework:spring-messaging:4.1.3.RELEASE"
    compile 'org.apache.tomcat.embed:tomcat-embed-websocket:8.0.15'
    compile 'org.apache.tomcat.embed:tomcat-embed-logging-juli:8.0.15'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.1"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

bintray {
    user = bintray_user
    key = bintray_key

    publications = ['mavenJava'] //When uploading Maven-based publication files

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'grooscript'
        name = "${project.group}:grooscript-gradle-plugin"
        desc = 'Convert groovy code to javascript'
        websiteUrl = 'http://grooscript.org/'
        issueTrackerUrl = 'https://github.com/chiquitinxx/grooscript-gradle-plugin/issues'
        vcsUrl = 'https://github.com/chiquitinxx/grooscript-gradle-plugin.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'javascript', 'grooscript', 'gradle', 'plugin']
        version {
            name = project.version
            desc = 'Websocket features'
            vcsTag = project.version
            attributes = [
                    'gradle-plugin': ['org.grooscript.conversion', project.group, project.name].join(':')
            ]
        }
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}
