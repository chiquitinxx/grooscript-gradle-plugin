plugins {
    id "com.jfrog.bintray" version "1.2"
    id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'org.grooscript'
version = '1.2.1-SNAPSHOT'

ext {
    grooscriptVersion = '1.2.0'
    gradleVersion = '2.6'
}

def compatibilityVersion = 1.7
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.3') {
        exclude module: "groovy-all"
    }
    testCompile 'cglib:cglib-nodep:3.1'
    compile("org.grooscript:grooscript:${grooscriptVersion}") {
        exclude module: "groovy"
        exclude module: "groovy-json"
    }
    compile('org.codehaus.gpars:gpars:1.2.1') {
        exclude module: 'groovy-all'
    }
    compile "org.springframework:spring-websocket:4.1.4.RELEASE"
    compile "org.springframework:spring-messaging:4.1.4.RELEASE"
    compile 'org.apache.tomcat.embed:tomcat-embed-websocket:8.0.15'
    compile 'org.apache.tomcat.embed:tomcat-embed-logging-juli:8.0.15'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
}

task wrapper(type: Wrapper) {
    gradleVersion = project.ext['gradleVersion']
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''

    publications = ['mavenJava']

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'grooscript'
        name = "${project.group}:grooscript-gradle-plugin"
        desc = 'Convert groovy code to javascript'
        websiteUrl = 'http://grooscript.org/'
        issueTrackerUrl = 'https://github.com/chiquitinxx/grooscript-gradle-plugin/issues'
        vcsUrl = 'https://github.com/chiquitinxx/grooscript-gradle-plugin.git'
        licenses = ['Apache-2.0']
        labels = ['groovy', 'javascript', 'grooscript', 'gradle', 'plugin']
        version {
            name = project.version
            desc = 'Improve conversion options'
            vcsTag = project.version
            attributes = [
                    'gradle-plugin': ['org.grooscript.conversion', project.group, project.name].join(':')
            ]
        }
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

asciidoctor {
    sourceDir = project.file 'doc'
    sources { include 'starting_gradle.adoc', 'tasks.adoc', 'templating.adoc' }
    attributes 'plugin-version': version,
            'gradle-version': gradleVersion,
            'grooscript-version': grooscriptVersion,
            author: 'Jorge Franco',
            email: 'grooscript@gmail.com',
            'source-highlighter': 'prettify',
            icons: 'font',
            'index-file': 'index.html'
}
